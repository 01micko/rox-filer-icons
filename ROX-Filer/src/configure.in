dnl Process this file with autoconf to produce a configure script.
AC_INIT(main.c)
AC_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_CC

dnl Use -Wall if possible
changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

dnl Find GLib (needed for future tests)
AC_MSG_CHECKING(for GLib library)
if test -z "$GLIB_CONFIG"; then
  GLIB_CONFIG=glib-config
fi
GLIB_LIBS=`$GLIB_CONFIG --libs`
changequote(,)dnl
if [ -n "$GLIB_LIBS" ]; then
  AC_MSG_RESULT(yes - use '$GLIB_LIBS')
else
  AC_MSG_ERROR(can't run '$GLIB_CONFIG --libs' - make sure it is in your PATH)
fi
changequote([,])dnl

if test -z "$GTK_CONFIG"; then
  GTK_CONFIG=gtk-config
fi
AC_SUBST(GTK_CONFIG)

dnl Find ImLib
AC_MSG_CHECKING(for ImLib library)
IMLIB_LIBS=`imlib-config --libs-gdk 2> /dev/null`
changequote(,)dnl
if [ -n "$IMLIB_LIBS" ]; then
changequote([,])dnl
  AC_MSG_RESULT(yes - use '$IMLIB_LIBS')
  AC_DEFINE(HAVE_IMLIB)
  IMLIB_CFLAGS=`imlib-config --cflags-gdk`
else
  AC_MSG_RESULT(no - 'imlib-config --libs-gdk' failed)
  IMLIB_CFLAGS=""
fi
AC_SUBST(IMLIB_CFLAGS)
AC_SUBST(IMLIB_LIBS)

dnl Does file(1) support '-b' ?
AC_MSG_CHECKING(whether file -b works)
if file -b / > /dev/null 2> /dev/null
then
  AC_DEFINE(FILE_B_FLAG)
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
fi

dnl Checks for header files.
AC_PATH_X
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h mntent.h sys/ucred.h sys/mntent.h)
AC_CHECK_HEADERS(vfs.h locale.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_CHECK_FUNCS(gethostname unsetenv mkdir rmdir strdup strtol getopt_long)

dnl Check for the Midnight Commander VFS library
AC_CHECK_LIB(vfs, mc_stat, , , $GLIB_LIBS)

AC_OUTPUT(Makefile)
